TITLE:
Step3 - 3step LSAY
DATA:
FILE = "Step3.dat";
 
VARIABLE:
NAMES = ENJOY USEFUL LOGICAL JOB ADULT FEMALE MATH_IRT CPROB1 CPROB2 CPROB3 CPROB4
     N; 
 MISSING=.;
 nominal=N;
  usevar = n;
  classes = c(4);
  
  usevar = female math_irt;
ANALYSIS:
estimator = mlr; 
  type = mixture; 
  starts = 0;
MODEL:
%OVERALL%

math_irt on female; ! covariate as a predictor of the distal outcome
C on female (f1-f3);        ! covariate as predictor of C

   %C#1%
[n#1@7.784]; ! MUST EDIT if you do not have a 4-class model. 
[n#2@4.199];
[n#3@5.312];

[math_irt](m1);    ! conditional distal mean 
math_irt;          ! conditional distal variance (freely estimated)

%C#2%
[n#1@-2.228];
[n#2@1.087];
[n#3@-6.772];

[math_irt](m2);
math_irt;

%C#3%
[n#1@4.387];
[n#2@-0.53];
[n#3@6.07];

[math_irt](m3);
math_irt;

%C#4%
[n#1@-7];
[n#2@-3.236];
[n#3@-3.631];

[math_irt](m4);
math_irt; 
MODEL CONSTRAINT:
New (diff12 diff13 diff23 
    diff14 diff24 diff34);
  
    diff12 = m1-m2;  ! test pairwise distal mean differences
    diff13 = m1-m3;
    diff23 = m2-m3;
    diff14 = m1-m4;
    diff24 = m2-m4;
    diff34 = m3-m4;
MODEL TEST:
     ! omnibus test of distal means
    m1=m2;
    m2=m3;
    m3=m4;
    !f1=f2;
    !f2=f3;
    !f3=f4;
 
